$title *medea* _ an economic model of the electricity system in Europe

$ontext
Abstract power system model *medea*, written by Sebastian Wehrle and Johannes Schmidt.
Data for concrete model instantiation is generated by the accompanying
python scripts /instantion/preprocess.py and /instantiation/instantiate.py

Full model code, data and licensing information provided at:
https://github.com/medea

For comments, suggestions, bug reporting and other correspondence please write to
sebastian.wehrle@boku.ac.at
$offtext

$onEoLCom
$EoLCom #

*# DEVELOPMENT:
*- re-check data for NTC
*- allow for investment in grid capacity

********************************************************************************
********** set declaration
********************************************************************************
sets
         f                               fuels
         l                               limits to feasible operating region
         prd                             products
         props                           technical properties of hydro storage plants
         r                               regions
         tec                             power plant technologies
         tec_chp(tec)                    subset of CHP technologies
         tec_hsp                         hydro storage technologies
         tec_itm                         renewable generation technologies
         t                               time - hours      / t1*t8760 /
         start_t(t)                      hour for initial values
         end_t(t)                        end time of iteration
;

alias(r,rr);

********************************************************************************
********** parameter declaration
********************************************************************************
parameters
         ANCIL_SERVICE_LVL(r)            generation level required for provision of ancillary services
         CONSUMPTION(r,t,prd)            hourly consumption of power and heat [GW]
         EFFICIENCY(tec,prd,f)           electrical efficiency of power plant [%]
         EMISSION_INTENSITY(f)              specific emission factor of each fuel [kt CO2 per GWh fuel]
         EXPORT_FLOWS(r,t)               exports to regions not modelled [GW]
         FEASIBLE_INPUT(tec,l,f)         fuel requirement for feasible output generation []
         FEASIBLE_OUTPUT(tec,l,prd)      feasible combinations of outputs []
         GEN_PROFILE(r,t,tec_itm)        generation profile of intermittent technologies
         HSP_PROPERTIES(r,tec_hsp,props) technical properties of hydro storage plants
         IMPORT_FLOWS(r,t)               imports from regions not modelled [GW]
         INSTALLED_CAP_ITM(r,tec_itm)    installed intermittent capacities [GW]
         INSTALLED_CAP_THERM(r,tec)      installed thermal capacities [GW]
         INVESTCOST_ITM(tec_itm)         annuity of investment in 1 GW intermittent technology
         INVESTCOST_THERMAL(tec)         annuity of investment in 1 GW thermal generation technology
         MAX_EMISSIONS(r)                upper emission limit
         NTC(r,rr)                       net transfer capacity from region r to region rr
         NUM(r,tec)                      installed capacity per technology in 100 MW slices
         OM_FIXED_COST(tec)              quasifixed cost of operation & maintenance
         OM_VARIABLE_COST(tec)           variable cost of operation & maintenance
         PRICE_DA(t)                     observed electricity price on day-ahead market [k EUR per GWh]
         PRICE_EUA(t)                    price of emission allowances [k EUR per kt CO2]
         PRICE_FUEL(t,f)                 price of fuel [k EUR per GWh]
         RESERVOIR_INFLOWS(r,t,tec_hsp)  inflows to reservoirs of hydro storage plants [GW]
         SWITCH_INVEST_THERM             switch for investment in thermal units
         SWITCH_INVEST_ITM               switch for investment in intermittents
         WON_LIMIT                       upper limit on onshore wind deployment
;
* starting and ending values for intra-year iterations
parameters
         FINAL_STORAGE(r,t,tec_hsp)      final reservoir filling level
         INIT_GEN(r,t,tec,prd)           initial generation at iteration start
         INIT_PUMP(r,t,tec_hsp)          initial pumping level
         INIT_STORAGE(r,t,tec_hsp)       initial reservoir filling level
         INIT_TURB(r,t,tec_hsp)          initial generation of hydro storage plant at iteration start
;
********************************************************************************
********** data instantiation
********************************************************************************
$if NOT exist MEDEA_%scenario%_data.gdx  $gdxin medea_data
$if     exist MEDEA_%scenario%_data.gdx  $gdxin medea_%scenario%_data
$load  f l tec tec_chp tec_hsp tec_itm prd props r
$load  ANCIL_SERVICE_LVL CONSUMPTION EMISSION_INTENSITY EXPORT_FLOWS EFFICIENCY
$load  FEASIBLE_INPUT FEASIBLE_OUTPUT GEN_PROFILE HSP_PROPERTIES IMPORT_FLOWS
$load  INSTALLED_CAP_ITM INSTALLED_CAP_THERM INVESTCOST_ITM INVESTCOST_THERMAL
$load  NTC NUM OM_FIXED_COST OM_VARIABLE_COST PRICE_DA PRICE_EUA
$load  PRICE_FUEL RESERVOIR_INFLOWS SWITCH_INVEST_THERM SWITCH_INVEST_ITM
$load  WON_LIMIT
$gdxin

********************************************************************************
********** helper parameters
********************************************************************************
parameters
PEAK_LOAD(r),
PEAK_PROFILE(r,tec_itm);

PEAK_LOAD(r) = smax(t, CONSUMPTION(r,t,'power'));
PEAK_PROFILE(r,tec_itm) = smax(t, GEN_PROFILE(r,t,tec_itm) );

display PEAK_LOAD, PEAK_PROFILE;


********************************************************************************
********** variable declaration
********************************************************************************
variables
         syscost                         total system cost (to be minimized)
         cost(r)                         total cost per region
         emissions(r)                    total emissions from power generation
         flow(r,rr,t)                    electricity flow from r to rr (i.e. export if positive) [GW]
;
positive variables
         cc_weights(r,t,tec,l)           weights of co-generation convex combination
         cost_emission(r,t,tec)          emission cost
         cost_fuel(r,t,tec)              fuel cost
         cost_om(r,tec)                  operation & maintenance cost
         cost_invgen(r)                  cost of investment in generators
         cost_gridexpansion(r)           cost of transmission grid expansion
         decommission(r,tec)             plant decommissioning
         invest_res(r,tec_itm)           added capacity of intermittent technologies
         invest_thermal(r,tec)           thermal power plant investment
         q_curtail(r,t,prd)              unused renewable generation
         q_fueluse(r,t,tec,f)            fuel consumed
         q_gen(r,t,tec,prd)              energy generation
         q_nonserved(r,t,prd)            consumption for which there is no supply
         q_pump(r,t,tec_hsp)             electricity pumped (stored)
         q_turbine(r,t,tec_hsp)          electricity generation from hydro reservoirs
         res_level(r,t,tec_hsp)          energy stored in reservoir
         ntc_invest(r,rr)                investment in transmission capacity
;

********************************************************************************
******* equation declaration
********************************************************************************
equations
         objective                       total system cost calculation
         obj_costreg                     calculation of cost per region
         obj_fuelcost                    total fuel cost
         obj_emissioncost                total emission cost
         obj_omcost                      total O&M cost
         obj_invgencost                  total cost of investment in generators
         obj_gridcost                    total cost of transmission grid expansion
         SD_balance_el                   supply-demand balance electricity
         SD_balance_ht                   supply-demand balance heat
         caplim_generation               capacity limit on thermal generators
         nonchp_generation               fuel requirement of non-CHPs
         cc_a                            CHP fuel-output combination must be convex
         cc_b                            feasible output combinations of CHPs
         cc_c                            feasible fuel requirement of CHPs
         caplim_hspgen                   generation limit of hydro storages
         caplim_pumping                  pumping limit of pumped storages
         caplim_reservoir                reservoir limit of hydro storages
         reservoir_balance               reservoir balance of hydro storages
         emission_calculation            total CO2 emissions from fuel combustion
         decommission_limit              only active plants can be decommissioned
         ancillary_service               must-run for provision of ancillary services
         flow_balance                    imports are exports from elsewhere
         flow_constraint_a               capacity restriction on exports
         flow_constraint_b               capacity restriction on imports
         ntc_invest_symmetry             ntc investment increases capacity in both directions
         policy_100resbalance            policy constraint requiring 100% renewable electricity generation over year
         policy_balancedflows            policy constraint for balanced imports and exports
         policy_fixgen                   forces model to seek alternatives to subsidized thermal dispatch
;

********************************************************************************
******* model equations
* ------------------------------------------------------------------------------
* OBJECTIVES
* ------------------------------------------------------------------------------
objective..                      syscost
                                 =E=
                                 sum(r, cost(r))
                                 ;
obj_costreg(r)..                 cost(r)
                                 =E=
                                 sum((t,tec), cost_fuel(r,t,tec))
                                 + sum((t,tec), cost_emission(r,t,tec))
                                 + sum(tec, cost_om(r,tec))
                                 + cost_invgen(r)
                                 + 12500 * sum((t,prd), q_nonserved(r,t,prd))
                                 + 110 * sum((t,prd), q_curtail(r,t,prd))
                                 + 1000 * cost_gridexpansion(r)
                                 ;
obj_fuelcost(r,t,tec)..          cost_fuel(r,t,tec)
                                 =E=
                                 sum(f,PRICE_FUEL(t,f) * q_fueluse(r,t,tec,f))
                                 ;
obj_emissioncost(r,t,tec)..      cost_emission(r,t,tec)
                                 =E=
                                 sum(f, PRICE_EUA(t) * EMISSION_INTENSITY(f) * q_fueluse(r,t,tec,f))
                                 ;
obj_omcost(r,tec)..              cost_om(r,tec)
                                 =E=
                                 OM_FIXED_COST(tec) * (NUM(r,tec) - decommission(r,tec) + invest_thermal(r,tec))
                                 + sum((t,prd), OM_VARIABLE_COST(tec) * q_gen(r,t,tec,prd))
                                 ;
obj_invgencost(r)..              cost_invgen(r)
                                 =E=
                                 sum(tec, INVESTCOST_THERMAL(tec) * invest_thermal(r,tec))
                                 + sum(tec_itm, INVESTCOST_ITM(tec_itm) * invest_res(r,tec_itm))
                                 ;
obj_gridcost(r)..                cost_gridexpansion(r)
                                 =E=
                                 sum(rr, ntc_invest(r,rr)) / 2
                                 ;
* ------------------------------------------------------------------------------
* SUPPLY-DEMAND BALANCES
* ------------------------------------------------------------------------------
SD_balance_el(r,t)..
                                 sum(tec,q_gen(r,t,tec,'power'))
                                 + sum(tec_hsp, q_turbine(r,t,tec_hsp))
                                 + sum(tec_itm, GEN_PROFILE(r,t,tec_itm) * (INSTALLED_CAP_ITM(r,tec_itm) + invest_res(r,tec_itm)) )
                                 + IMPORT_FLOWS(r,t)
                                 - q_curtail(r,t,'power')
                                 + q_nonserved(r,t,'power')
                                 =E=
                                 CONSUMPTION(r,t,'power')
                                 + sum(tec_hsp, q_pump(r,t,tec_hsp))
                                 + EXPORT_FLOWS(r,t)
                                 + sum(rr, flow(r,rr,t) )
                                 ;
SD_balance_ht(r,t)..
                                 sum(tec,q_gen(r,t,tec,'heat'))
                                 + q_nonserved(r,t,'heat')
                                 =E=
                                 CONSUMPTION(r,t,'heat')
                                 ;
* ------------------------------------------------------------------------------
* THERMAL GENERATION
* ------------------------------------------------------------------------------
caplim_generation(r,t,tec,prd)..
                                 q_gen(r,t,tec,prd)
                                 =L=
                                 SMAX(l, FEASIBLE_OUTPUT(tec,l,prd)) * (NUM(r,tec) - decommission(r,tec) + invest_thermal(r,tec) )
                                 ;
nonchp_generation(r,t,tec)$(NOT tec_chp(tec))..
                                 sum(f, q_fueluse(r,t,tec,f)*EFFICIENCY(tec,'power',f) )
                                 =G=
                                 q_gen(r,t,tec,'power')
                                 ;
cc_a(r,t,tec)$tec_chp(tec)..
                                 Sum(l, cc_weights(r,t,tec,l))
                                 =E=
                                 (NUM(r,tec) - decommission(r,tec) + invest_thermal(r,tec) )
                                 ;
cc_b(r,t,tec,prd)$tec_chp(tec)..
                                 q_gen(r,t,tec,prd)
                                 =L=
                                 Sum(l, cc_weights(r,t,tec,l) * FEASIBLE_OUTPUT(tec,l,prd))
                                 ;
cc_c(r,t,tec,f)$tec_chp(tec)..
                                 q_fueluse(r,t,tec,f)
                                 =G=
                                 Sum(l, cc_weights(r,t,tec,l) * FEASIBLE_INPUT(tec,l,f) )
                                 ;
* ------------------------------------------------------------------------------
* HYDRO STORAGE GENERATION
* ------------------------------------------------------------------------------
caplim_hspgen(r,t,tec_hsp)..
                                 q_turbine(r,t,tec_hsp)
                                 =L=
                                 HSP_PROPERTIES(r,tec_hsp,'cap_turbine')
                                 ;
caplim_pumping(r,t,tec_hsp)..
                                 q_pump(r,t,tec_hsp)
                                 =L=
                                 HSP_PROPERTIES(r,tec_hsp,'cap_pump')
                                 ;
caplim_reservoir(r,t,tec_hsp)..
                                 res_level(r,t,tec_hsp)
                                 =L=
                                 HSP_PROPERTIES(r,tec_hsp,'cap_reservoir')
                                 ;
reservoir_balance(r,t,tec_hsp)$(ord(t) > 1 AND HSP_PROPERTIES(r,tec_hsp,'efficiency_turbine'))..
                                 res_level(r,t,tec_hsp) - res_level(r,t-1,tec_hsp)
                                 =E=
                                 RESERVOIR_INFLOWS(r,t,tec_hsp)
                                 + q_pump(r,t,tec_hsp) * HSP_PROPERTIES(r,tec_hsp,'efficiency_pump')
                                 - q_turbine(r,t,tec_hsp) / HSP_PROPERTIES(r,tec_hsp,'efficiency_turbine')
                                 ;
* ------------------------------------------------------------------------------
* international commercial electricity exchange
* ------------------------------------------------------------------------------
flow_balance(r,rr,t)$NTC(r,rr)..
                                 flow(r,rr,t)
                                 =E=
                                 -flow(rr,r,t)
                                 ;
flow_constraint_a(r,rr,t)$NTC(r,rr)..
                                 flow(r,rr,t)
                                 =L=
                                 NTC(r,rr) + ntc_invest(r,rr)
                                 ;
flow_constraint_b(r,rr,t)$NTC(rr,r)..
                                 flow(r,rr,t)
                                 =G=
                                 - (NTC(rr,r) + ntc_invest(rr,r))
                                 ;
ntc_invest_symmetry(r,rr)..      ntc_invest(r,rr)
                                 =E=
                                 ntc_invest(rr,r)
                                 ;
* no flows from region to itself
flow.FX(r,rr,t)$(not NTC(r,rr))   = 0;
flow.FX(rr,r,t)$(not NTC(rr,r))   = 0;
* ------------------------------------------------------------------------------
* emissions
* ------------------------------------------------------------------------------
emission_calculation(r)..
                                 emissions(r)
                                 =E=
                                 sum((t,f), EMISSION_INTENSITY(f) * sum(tec,q_fueluse(r,t,tec,f)))
                                 ;
* ------------------------------------------------------------------------------
* decommissioning
* ------------------------------------------------------------------------------
decommission_limit(r,tec)..
                                 decommission(r,tec)
                                 =L=
                                 NUM(r,tec) + invest_thermal(r,tec)
                                 ;
* ------------------------------------------------------------------------------
* ancillary services
* ------------------------------------------------------------------------------
ancillary_service(r,t)..
                                 sum(tec$(NOT tec_chp(tec)), q_gen(r,t,tec,'power'))
                                 + sum(tec_hsp, q_turbine(r,t,tec_hsp))
                                 + sum(tec_hsp, q_pump(r,t,tec_hsp))
                                 =G=
                                 0.125 * PEAK_LOAD(r)
                                 + 0.075 * sum(tec_itm$(NOT SAMEAS(tec_itm,'ror')),
                                 PEAK_PROFILE(r,tec_itm) * (INSTALLED_CAP_ITM(r,tec_itm) + invest_res(r,tec_itm))
                                 );
* ------------------------------------------------------------------------------
* policy constraints
* ------------------------------------------------------------------------------
policy_100resbalance..           sum((t,tec_itm), GEN_PROFILE('AT',t,tec_itm) * (INSTALLED_CAP_ITM('AT',tec_itm) + invest_res('AT',tec_itm)) )
                                 + sum((t,tec_hsp), q_turbine('AT',t,tec_hsp))
                                 - sum((t,tec_hsp), q_pump('AT',t,tec_hsp) * HSP_PROPERTIES('AT',tec_hsp,'efficiency_pump'))
                                 - sum(t, q_curtail('AT',t,'power'))
                                 =G=
                                 0.88 * sum(t, CONSUMPTION('AT',t,'power'))
                                 ;
policy_balancedflows..           sum((rr,t), flow('AT',rr,t))
                                 - sum(t, IMPORT_FLOWS('AT',t))
                                 + sum(t, EXPORT_FLOWS('AT',t))
                                 =E=
                                 0
                                 ;
policy_fixgen..                  sum((t,tec), q_gen('AT',t,tec,'power') )
                                 =L=
                                 25348.620
                                 ;
* ------------------------------------------------------------------------------
* switches for long-term vs short-term model version
* ------------------------------------------------------------------------------
invest_thermal.UP(r,tec) =       SWITCH_INVEST_THERM;
decommission.UP(r,tec) =         SWITCH_INVEST_THERM;
invest_res.UP(r,tec_itm) =       SWITCH_INVEST_ITM;
invest_res.FX(r,'ror') =         0;
invest_res.UP(r,'wind_on') =     WON_LIMIT;

* ------------------------------------------------------------------------------
* restrict fuel use according to technology
* ------------------------------------------------------------------------------
q_fueluse.UP(r,t,tec,f)$(NOT sum(prd,EFFICIENCY(tec,prd,f))) = 0;

model medea / all - policy_fixgen /;

********************************************************************************
******* set starting values
*q_gen.FX(r,start_t,tec,prd)      $INIT_GEN(r,start_t,tec,prd)            = INIT_GEN(r,start_t,tec,prd);
*q_pump.FX(r,start_t,tec_hsp)     $INIT_PUMP(r,start_t,tec_hsp)           = INIT_PUMP(r,start_t,tec_hsp);
*q_turbine.FX(r,start_t,tec_hsp)  $INIT_TURB(r,start_t,tec_hsp)           = INIT_TURB(r,start_t,tec_hsp);
*res_level.FX(r,start_t,tec_hsp)  $INIT_STORAGE(r,start_t,tec_hsp)        = INIT_STORAGE(r,start_t,tec_hsp);
*res_level.FX(r,end_t,tec_hsp)    $FINAL_STORAGE(r,end_t,tec_hsp)         = FINAL_STORAGE(r,end_t,tec_hsp);

options
LP = OSIGurobi,
reslim = 54000,
threads = 8,
optCR = 0.01,
BRatio = 1
;

solve medea using LP minimizing syscost;


********************************************************************************
******* reporting

******* solution details
scalars modelStat, solveStat;
modelStat = medea.modelstat;
solveStat = medea.solvestat;

******* system operations
parameter
annual_generation(r,prd),
annual_pumping(r),
annual_turbining(r),
annual_netflow(r),
annual_fixedexports,
annual_fixedimports,
annual_curtail(r);

annual_generation(r,prd) = sum((t,tec), q_gen.L(r, t, tec, prd));
annual_pumping(r) = sum((t,tec_hsp), q_pump.L(r,t,tec_hsp));
annual_turbining(r) = sum((t,tec_hsp), q_turbine.L(r,t,tec_hsp));
annual_netflow(r) = sum(t, flow.L('AT',r,t));
annual_fixedexports = sum(t, EXPORT_FLOWS('AT',t));
annual_fixedimports = sum(t, IMPORT_FLOWS('AT',t));
annual_curtail(r) = sum(t, q_curtail.L(r,t,'power'));

display annual_generation, annual_netflow, annual_fixedexports, annual_fixedimports, annual_curtail;

******* prices, cost, producer surplus
parameter
annual_price_el(r),
annual_price_ht(r),
annual_cost(r,tec),
annual_surplus_therm(r,tec),
annual_surplus_stor(r,tec_hsp),
producer_surplus(r);

annual_price_el(r) = sum(t, SD_balance_el.M(r,t))/card(t);
annual_price_ht(r) = sum(t, SD_balance_ht.M(r,t))/card(t);
annual_cost(r,tec) = sum(t, cost_fuel.L(r,t,tec)
                         + cost_emission.L(r,t,tec)
                         + sum(prd, OM_VARIABLE_COST(tec) * q_gen.L(r,t,tec,prd)));
annual_surplus_therm(r,tec) =   sum(t,
                         SD_balance_el.M(r,t) * q_gen.L(r,t,tec,'power')
                         + SD_balance_ht.M(r,t) * q_gen.L(r,t,tec,'heat')
                         - cost_fuel.L(r,t,tec)
                         - cost_emission.L(r,t,tec)
                         - sum(prd, OM_VARIABLE_COST(tec) * q_gen.L(r,t,tec,prd))
                         );
annual_surplus_stor(r,tec_hsp) = sum(t,
                         SD_balance_el.M(r,t) * q_turbine.L(r,t,tec_hsp)
                         - SD_balance_el.M(r,t) * q_pump.L(r,t,tec_hsp)
                         );
producer_surplus(r) =    sum(tec, annual_surplus_therm(r,tec))
                         + sum(tec_hsp, annual_surplus_stor(r,tec_hsp))
                         ;

display
annual_price_el, annual_price_ht, annual_cost, annual_surplus_therm, annual_surplus_stor, producer_surplus;

parameter av_cap(r, tec, prd);
av_cap(r,tec,prd) = smax(l, FEASIBLE_OUTPUT(tec,l,prd)) * NUM(r,tec);
display NUM, av_cap;
