$title *medea* _ an economic model of the electricity system in Europe

$ontext
Abstract power system model *medea*, written by Sebastian Wehrle and Johannes Schmidt.
Data for concrete model instantiation is generated by the accompanying
python script instantiate.py

With additional, inserted model code, medea was used for:
* District heating systems under high CO2 prices: the role of the pass-through from emission cost to electricity prices:
  medea_dhpt.gms
* The cost of undistrubed landscapes:
  medea_udls.gms
* Austrian policy analysis:
  medea_pobu.gms

# TO-DO: scatter of conventional generation + psp pumping & turbining against
#        renewables max and peak load
#        to check assumptions in ancillary services equation (minimal generation)
#        - source for cost grid expansion
#        - source for cost of lost load

Full model code, data and licensing information provided at:
https://github.com/inwe-energy/medea

For comments, suggestions, bug reporting and other correspondence please write to
sebastian.wehrle@boku.ac.at
$offtext

$onEoLCom
$EoLCom #


********************************************************************************
********** set declaration
********************************************************************************
sets
         f                                       fuels
         l                                       limits to feasible operating region
         prd                                     products
         props                                   technical properties of hydro storage plants
         tec                                     power plant technologies
         tec_chp(tec)                            subset of CHP technologies
         tec_pth(tec)                            subset of power to heat technologies
         tec_strg                                 hydro storage technologies
         tec_itm                                 renewable generation technologies
         t                                       time - hours
         z                                       market zones
         start_t(t)                              hour for initial values
         end_t(t)                                end time of iteration
;

alias(z,zz);

********************************************************************************
********** parameter declaration
********************************************************************************
parameters
         ANCIL_SERVICE_LVL(z)                    generation level required for provision of ancillary services
         CONSUMPTION(z,t,prd)                    hourly consumption of power and heat [GW]
         EFFICIENCY(tec,prd,f)                   electrical efficiency of power plant [%]
         EMISSION_INTENSITY(f)                   specific emission factor of each fuel [kt CO2 per GWh fuel]
         FEASIBLE_INPUT(tec,l,f)                 fuel requirement for feasible output generation []
         FEASIBLE_OUTPUT(tec,l,prd)              feasible combinations of outputs []
         GEN_PROFILE(z,t,tec_itm)                generation profile of intermittent technologies
         FLOW_EXPORT(z,t)                        exports to market zones not modelled [GW]
         FLOW_IMPORT(z,t)                        imports from market zones not modelled [GW]
         INSTALLED_CAP_ITM(z,tec_itm)            installed intermittent capacities [GW]
         INSTALLED_CAP_THERM(z,tec)              installed thermal capacities [GW]
         INVESTCOST_ITM(z,tec_itm)               annuity of investment in 1 GW intermittent technology
         INVESTCOST_THERMAL(tec)                 annuity of investment in 1 GW thermal generation technology
         MAX_EMISSIONS(z)                        upper emission limit
         ATC(z,zz)                               net transfer capacity from market zone z to market zone zz
         OM_FIXED_COST(tec)                      quasifixed cost of operation & maintenance
         OM_VARIABLE_COST(tec)                   variable cost of operation & maintenance
         PRICE_DA(t,z)                           observed electricity price on day-ahead market [k EUR per GWh]
         PRICE_CO2(t,z)                          price of emission allowances [k EUR per kt CO2]
         PRICE_FUEL(t,z,f)                       price of fuel [k EUR per GWh]
         RESERVOIR_INFLOWS(z,t,tec_strg)         inflows to reservoirs of hydro storage plants [GW]
         STORAGE_PROPERTIES(z,tec_strg,props)    technical properties of electricity storages
         VALUE_NSE(z)                            value of non-served energy
         SWITCH_INVEST_THERM                     switch for investment in thermal units
         SWITCH_INVEST_ITM(z,tec_itm)            switch for investment in intermittents
         SWITCH_INVEST_STORAGE(z, tec_strg)      switch for investment in storage technologies
         SWITCH_INVEST_ATC(z,zz)                 switch for investment in interconnectors
;
* starting and ending values (mostly for intra-year iterations)
parameters
         FINAL_STORAGE(z,t,tec_strg)              final reservoir filling level
         INIT_GEN(z,t,tec,prd)                    initial generation at iteration start
         INIT_PUMP(z,t,tec_strg)                  initial pumping level
         INIT_STORAGE(z,t,tec_strg)               initial reservoir filling level
         INIT_TURB(z,t,tec_strg)                  initial generation of hydro storage plant at iteration start
;
********************************************************************************
********** data instantiation
********************************************************************************
$if NOT exist MEDEA_%scenario%_data.gdx  $gdxin medea_main_data
$if     exist MEDEA_%scenario%_data.gdx  $gdxin medea_%scenario%_data
$load  f l t tec tec_chp tec_strg tec_itm tec_pth prd props z
$load  ANCIL_SERVICE_LVL CONSUMPTION EMISSION_INTENSITY FLOW_EXPORT EFFICIENCY
$load  FEASIBLE_INPUT FEASIBLE_OUTPUT GEN_PROFILE STORAGE_PROPERTIES FLOW_IMPORT
$load  INSTALLED_CAP_ITM INSTALLED_CAP_THERM INVESTCOST_ITM INVESTCOST_THERMAL
$load  ATC OM_FIXED_COST OM_VARIABLE_COST PRICE_DA PRICE_CO2
$load  PRICE_FUEL RESERVOIR_INFLOWS SWITCH_INVEST_THERM SWITCH_INVEST_ITM
$load  SWITCH_INVEST_STORAGE SWITCH_INVEST_ATC
$gdxin

VALUE_NSE(z) = 12500;

********************************************************************************
********** helper parameters
********************************************************************************
parameters
PEAK_LOAD(z),
PEAK_PROFILE(z,tec_itm);

PEAK_LOAD(z) = smax(t, CONSUMPTION(z,t,'el'));
PEAK_PROFILE(z,tec_itm) = smax(t, GEN_PROFILE(z,t,tec_itm) );

display PEAK_LOAD, PEAK_PROFILE;

********************************************************************************
********** variable declaration
********************************************************************************
variables
         syscost                                 total system cost (to be minimized)
         cost(z)                                 total cost per market zone
         emissions(z)                            total emissions from power generation
         flow(z,zz,t)                            electricity flow from z to zz (i.e. export if positive) [GW]
;
positive variables
         cc_weights(z,t,tec,l)                   weights of co-generation convex combination
         cost_emission(z,t,tec)                  emission cost
         cost_fuel(z,t,tec)                      fuel cost
         cost_om(z,tec)                          operation & maintenance cost
         cost_invgen(z)                          cost of investment in generators
         cost_invstrg(z)                         cost of investment in storages
         cost_gridexpansion(z)                   cost of transmission grid expansion
         decommission(z,tec)                     plant decommissioning
         invest_res(z,tec_itm)                   added capacity of intermittent technologies
         invest_thermal(z,tec)                   thermal power plant investment
         invest_storage_power(z,tec_strg)        invested storage power (charge - discharge)
         invest_storage_energy(z,tec_strg)       invested storage energy
         q_curtail(z,t)                          unused renewable generation
         q_fueluse(z,t,tec,f)                    fuel consumed
         q_gen(z,t,tec,prd)                      energy generation of dispatchable units
         q_itm(z,t,tec_itm)                      electricity generation from intermittent sources
         q_nonserved(z,t,prd)                    consumption for which there is no supply
         q_store_in(z,t,tec_strg)                electricity stored
         q_store_out(z,t,tec_strg)               electricity generated from storages
         storage_level(z,t,tec_strg)             energy stored in storages
         invest_atc(z,zz)                        investment in transmission capacity
;

********************************************************************************
******* equation declaration
********************************************************************************
equations
         objective                               total system cost calculation
         obj_costreg                             calculation of cost per market zone
         obj_fuelcost                            total fuel cost
         obj_emissioncost                        total emission cost
         obj_omcost                              total O&M cost
         obj_invgencost                          total cost of investment in generators
         obj_invstoragecost(z)                   total cost of storage investment
         obj_gridcost                            total cost of transmission grid expansion
         SD_balance_el                           supply-demand balance electricity
         SD_balance_ht                           supply-demand balance heat
         itm_generation                          generation from intermittent sources
         caplim_generation                       capacity limit on thermal generators
         nonchp_generation                       fuel requirement of non-CHPs
         pth_generation                          fuel requirement of power to heat units
         cc_a                                    CHP fuel-output combination must be convex
         cc_b                                    feasible output combinations of CHPs
         cc_c                                    feasible fuel requirement of CHPs
         storelim_out                            generation limit of storages
         storelim_in                             charging limit of storages
         storelim_storage                        energy limit of hydro storages
         storage_balance                         electricity balance of storages
         storage_sizing                          size energy storage to store at least one hour
         emission_calculation                    total CO2 emissions from fuel combustion
         decommission_limit                      only active plants can be decommissioned
         ancillary_service                       must-run for provision of ancillary services
         curtail_limit                           only generation from intermittent sources can be curtailed
         flow_balance                            imports are exports from elsewhere
         flow_constraint_a                       capacity restriction on exports
         flow_constraint_b                       capacity restriction on imports
         atc_invest_symmetry                     atc investment increases capacity in both directions
;

********************************************************************************
******* model equations
* ------------------------------------------------------------------------------
* OBJECTIVES
* ------------------------------------------------------------------------------
objective..                      syscost
                                 =E=
                                 sum(z, cost(z))
                                 ;
obj_costreg(z)..                 cost(z)
                                 =E=
                                 sum((t,tec), cost_fuel(z,t,tec))
                                 + sum((t,tec), cost_emission(z,t,tec))
                                 + sum(tec, cost_om(z,tec))
                                 + cost_invgen(z)
                                 + cost_invstrg(z)
                                 + VALUE_NSE(z) * sum((t,prd), q_nonserved(z,t,prd))
                                 + 2000 * cost_gridexpansion(z)
                                 ;
obj_fuelcost(z,t,tec)..          cost_fuel(z,t,tec)
                                 =E=
                                 sum(f, PRICE_FUEL(t,z,f) * q_fueluse(z,t,tec,f))
                                 ;
obj_emissioncost(z,t,tec)..      cost_emission(z,t,tec)
                                 =E=
                                 sum(f, PRICE_CO2(t,z) * EMISSION_INTENSITY(f) * q_fueluse(z,t,tec,f))
                                 ;
obj_omcost(z,tec)..              cost_om(z,tec)
                                 =E=
                                 OM_FIXED_COST(tec) * (INSTALLED_CAP_THERM(z,tec) - decommission(z,tec) + invest_thermal(z,tec))
                                 + sum((t,prd), OM_VARIABLE_COST(tec) * q_gen(z,t,tec,prd))
                                 ;
obj_invgencost(z)..              cost_invgen(z)
                                 =E=
                                 sum(tec, INVESTCOST_THERMAL(tec) * invest_thermal(z,tec))
                                 + sum(tec_itm, INVESTCOST_ITM(z,tec_itm) * invest_res(z,tec_itm))
                                 ;
obj_invstoragecost(z)..          cost_invstrg(z)
                                 =E=
                                 sum(tec_strg,
                                 invest_storage_power(z,tec_strg) * STORAGE_PROPERTIES(z,tec_strg,'cost_power')
                                 + invest_storage_energy(z,tec_strg) * STORAGE_PROPERTIES(z,tec_strg,'cost_energy')
                                 );
obj_gridcost(z)..                cost_gridexpansion(z)
                                 =E=
                                 sum(zz, invest_atc(z,zz)) / 2
                                 ;
* ------------------------------------------------------------------------------
* SUPPLY-DEMAND BALANCES
* ------------------------------------------------------------------------------
SD_balance_el(z,t)..
                                 sum(tec,q_gen(z,t,tec,'el'))
                                 + sum(tec_strg, q_store_out(z,t,tec_strg))
                                 + sum(tec_itm, q_itm(z,t,tec_itm) )
                                 + FLOW_IMPORT(z,t)
                                 + q_nonserved(z,t,'el')
                                 =E=
                                 CONSUMPTION(z,t,'el')
                                 + sum(tec, q_fueluse(z,t,tec,'Power'))
                                 + sum(tec_strg, q_store_in(z,t,tec_strg))
                                 + FLOW_EXPORT(z,t)
                                 + sum(zz, flow(z,zz,t) )
                                 + q_curtail(z,t)
                                 ;
SD_balance_ht(z,t)..
                                 sum(tec,q_gen(z,t,tec,'ht'))
                                 + q_nonserved(z,t,'ht')
                                 =E=
                                 CONSUMPTION(z,t,'ht')
                                 ;
* ------------------------------------------------------------------------------
* INTERMITTENT GENERATION
* ------------------------------------------------------------------------------
itm_generation(z,t,tec_itm)..
                                 q_itm(z,t,tec_itm)
                                 =E=
                                 GEN_PROFILE(z,t,tec_itm) * (INSTALLED_CAP_ITM(z,tec_itm) + invest_res(z,tec_itm))
                                 ;
* ------------------------------------------------------------------------------
* THERMAL GENERATION
* ------------------------------------------------------------------------------
caplim_generation(z,t,tec,prd)..
                                 q_gen(z,t,tec,prd)
                                 =L=
                                 INSTALLED_CAP_THERM(z,tec) - decommission(z,tec) + invest_thermal(z,tec)
                                 ;
nonchp_generation(z,t,tec)$(NOT tec_chp(tec))..
                                 sum(f, q_fueluse(z,t,tec,f) * EFFICIENCY(tec,'el',f) )
                                 =E=
                                 q_gen(z,t,tec,'el')
                                 ;
pth_generation(z,t,tec)$(tec_pth(tec))..
* replace sum(f,.) with q_fueluse(z,t,tec,'el') * EFFICIENCY(tec,'ht','el') ?
                                 sum(f, q_fueluse(z,t,tec,f) * EFFICIENCY(tec,'ht',f) )
                                 =E=
                                 q_gen(z,t,tec,'ht')
                                 ;
cc_a(z,t,tec)$tec_chp(tec)..
                                 Sum(l, cc_weights(z,t,tec,l))
                                 =E=
                                 INSTALLED_CAP_THERM(z,tec) - decommission(z,tec) + invest_thermal(z,tec)
                                 ;
cc_b(z,t,tec,prd)$tec_chp(tec)..
                                 q_gen(z,t,tec,prd)
                                 =L=
                                 Sum(l, cc_weights(z,t,tec,l) * FEASIBLE_OUTPUT(tec,l,prd))
                                 ;
cc_c(z,t,tec,f)$tec_chp(tec)..
                                 q_fueluse(z,t,tec,f)
                                 =G=
                                 Sum(l, cc_weights(z,t,tec,l) * FEASIBLE_INPUT(tec,l,f) )
                                 ;

* ------------------------------------------------------------------------------
* ELECTRICITY STORAGES
* ------------------------------------------------------------------------------
storelim_out(z,t,tec_strg)..
                                 q_store_out(z,t,tec_strg)
                                 =L=
                                 STORAGE_PROPERTIES(z,tec_strg,'power_out')
                                 + invest_storage_power(z,tec_strg)
                                 ;
storelim_in(z,t,tec_strg)..
                                 q_store_in(z,t,tec_strg)
                                 =L=
                                 STORAGE_PROPERTIES(z,tec_strg,'power_in')
                                 + invest_storage_power(z,tec_strg)
                                 ;
storelim_storage(z,t,tec_strg)..
                                 storage_level(z,t,tec_strg)
                                 =L=
                                 STORAGE_PROPERTIES(z,tec_strg,'energy_max')
                                 + invest_storage_energy(z,tec_strg)
                                 ;
storage_balance(z,t,tec_strg)$(ord(t) > 1 AND STORAGE_PROPERTIES(z,tec_strg,'efficiency_out'))..
                                 storage_level(z,t,tec_strg)
                                 =E=
                                 storage_level(z,t-1,tec_strg)
                                 + RESERVOIR_INFLOWS(z,t,tec_strg)
                                 + q_store_in(z,t,tec_strg) * STORAGE_PROPERTIES(z,tec_strg,'efficiency_in')
                                 - q_store_out(z,t,tec_strg) / STORAGE_PROPERTIES(z,tec_strg,'efficiency_out')
                                 ;
storage_sizing(z,tec_strg)..
                                 invest_storage_energy(z,tec_strg)
                                 =G=
                                 invest_storage_power(z,tec_strg)
                                 ;

* ------------------------------------------------------------------------------
* international commercial electricity exchange
* ------------------------------------------------------------------------------
flow_balance(z,zz,t)$ATC(z,zz)..
                                 flow(z,zz,t)
                                 =E=
                                 -flow(zz,z,t)
                                 ;
flow_constraint_a(z,zz,t)$ATC(z,zz)..
                                 flow(z,zz,t)
                                 =L=
                                 ATC(z,zz) + invest_atc(z,zz)
                                 ;
flow_constraint_b(z,zz,t)$ATC(zz,z)..
                                 flow(z,zz,t)
                                 =G=
                                 - (ATC(zz,z) + invest_atc(zz,z))
                                 ;
atc_invest_symmetry(z,zz)..      invest_atc(z,zz)
                                 =E=
                                 invest_atc(zz,z)
                                 ;
* no flows from market zone to itself
flow.FX(z,zz,t)$(not ATC(z,zz))   = 0;
flow.FX(zz,z,t)$(not ATC(zz,z))   = 0;
* ------------------------------------------------------------------------------
* emissions
* ------------------------------------------------------------------------------
emission_calculation(z)..
                                 emissions(z)
                                 =E=
                                 sum((t,f), EMISSION_INTENSITY(f) * sum(tec,q_fueluse(z,t,tec,f)))
                                 ;
* ------------------------------------------------------------------------------
* decommissioning
* ------------------------------------------------------------------------------
decommission_limit(z,tec)..
                                 decommission(z,tec)
                                 =L=
                                 INSTALLED_CAP_THERM(z,tec) + invest_thermal(z,tec)
                                 ;
* ------------------------------------------------------------------------------
* ancillary services
* ------------------------------------------------------------------------------
ancillary_service(z,t)..
                                 sum(tec$(NOT tec_chp(tec)), q_gen(z,t,tec,'el'))
                                 + sum(tec_strg, q_store_out(z,t,tec_strg))
                                 + sum(tec_strg, q_store_in(z,t,tec_strg))
                                 =G=
                                 0.175 * PEAK_LOAD(z)  # 0.125
                                 + 0.15 * sum(tec_itm$(NOT SAMEAS(tec_itm,'ror')),  # 0.075
                                 PEAK_PROFILE(z,tec_itm) * (INSTALLED_CAP_ITM(z,tec_itm) + invest_res(z,tec_itm))
                                 );
* ------------------------------------------------------------------------------
* curtail of intermittent generation only
* ------------------------------------------------------------------------------
curtail_limit(z,t)..             q_curtail(z,t)
                                 =L=
                                 sum(tec_itm$(NOT SAMEAS(tec_itm,'ror')),
                                     GEN_PROFILE(z,t,tec_itm) * (INSTALLED_CAP_ITM(z,tec_itm) + invest_res(z,tec_itm))
                                 );
* ------------------------------------------------------------------------------
* additional constraints
* ------------------------------------------------------------------------------

* restrict fuel use according to technology
q_fueluse.UP(z,t,tec,f)$(NOT sum(prd,EFFICIENCY(tec,prd,f))) = 0;

* bounds on investment -- long-term vs short-term model
invest_thermal.UP(z,tec) =       SWITCH_INVEST_THERM;
decommission.UP(z,tec) =         SWITCH_INVEST_THERM;
invest_res.UP(z,tec_itm) =       SWITCH_INVEST_ITM(z, tec_itm);
invest_storage_power.UP(z,tec_strg) = SWITCH_INVEST_STORAGE(z, tec_strg);
invest_storage_energy.UP(z,tec_strg) = SWITCH_INVEST_STORAGE(z, tec_strg);
invest_atc.UP(z,zz) =            SWITCH_INVEST_ATC(z,zz);

* ==============================================================================
* include specific changes
* ==============================================================================
$if not set project $goto next
$include medea_%project%.gms
* ==============================================================================
$label next

model medea / all /;

********************************************************************************
******* set starting values
*q_gen.FX(z,start_t,tec,prd)      $INIT_GEN(z,start_t,tec,prd)            = INIT_GEN(z,start_t,tec,prd);
*q_store_in.FX(z,start_t,tec_strg)     $INIT_PUMP(z,start_t,tec_strg)           = INIT_PUMP(z,start_t,tec_strg);
*q_store_out.FX(z,start_t,tec_strg)  $INIT_TURB(z,start_t,tec_strg)           = INIT_TURB(z,start_t,tec_strg);
*res_level.FX(z,start_t,tec_strg)  $INIT_STORAGE(z,start_t,tec_strg)        = INIT_STORAGE(z,start_t,tec_strg);
*res_level.FX(z,end_t,tec_strg)    $FINAL_STORAGE(z,end_t,tec_strg)         = FINAL_STORAGE(z,end_t,tec_strg);

options
*LP = OSIGurobi,
reslim = 54000,
threads = 8,
optCR = 0.01,
BRatio = 1
;

$onecho > osigurobi.opt
workerPool nora.boku.ac.at:9797
workerPassword keepulbooleatias
ConcurrentJobs 2
method 1
$offecho

*medea.OptFile = 1;

solve medea using LP minimizing syscost;

********************************************************************************
******* reporting

******* solution details
scalars modelStat, solveStat;
modelStat = medea.modelstat;
solveStat = medea.solvestat;

****** exogenous parameters
parameters
ANNUAL_CONSUMPTION(z,prd),
FULL_LOAD_HOURS(z,tec_itm),
AVG_PRICE(z,f),
AVG_PRICE_DA(z),
AVG_PRICE_EUA(z);

ANNUAL_CONSUMPTION(z,prd) = sum(t, CONSUMPTION(z,t,prd));
FULL_LOAD_HOURS(z,tec_itm) = sum(t, GEN_PROFILE(z,t,tec_itm));
AVG_PRICE(z,f) = sum(t, PRICE_FUEL(t,z,f)) / card(t);
AVG_PRICE_DA(z) = sum(t, PRICE_DA(t,z)) / card(t);
AVG_PRICE_EUA(z) = sum(t, PRICE_CO2(t,z)) / card(t);

display ANNUAL_CONSUMPTION, FULL_LOAD_HOURS, AVG_PRICE, AVG_PRICE_DA, AVG_PRICE_EUA

******* system operations
parameter
annual_generation(z,prd),
annual_generation_by_tec(z,tec,prd),
annual_pumping(z),
annual_turbining(z),
annual_netflow(z),
annual_fueluse(z,f),
annual_fixedexports,
annual_fixedimports,
annual_curtail(z);

annual_generation(z,prd) = sum((t,tec), q_gen.L(z, t, tec, prd));
annual_generation_by_tec(z,tec,prd) = sum(t, q_gen.L(z, t, tec, prd));
annual_pumping(z) = sum((t,tec_strg), q_store_in.L(z,t,tec_strg));
annual_turbining(z) = sum((t,tec_strg), q_store_out.L(z,t,tec_strg));
annual_netflow(zz) = sum(t, flow.L('AT',zz,t));
annual_fueluse(z,f) = sum((t,tec), q_fueluse.L(z,t,tec,f));
annual_fixedexports = sum(t, FLOW_EXPORT('AT',t));
annual_fixedimports = sum(t, FLOW_IMPORT('AT',t));
annual_curtail(z) = sum(t, q_curtail.L(z,t));

display annual_generation, annual_netflow, annual_fueluse, annual_fixedexports, annual_fixedimports, annual_curtail;

******* annual values
parameters
ann_value_generation(z,prd),
ann_value_generation_by_tec(z,tec,prd),
ann_value_pumping(z),
ann_value_turbining(z),
ann_value_flows(z,zz),
ann_value_curtail(z);

ann_value_generation(z,prd) = sum((t,tec), SD_balance_el.M(z,t) * q_gen.L(z, t, tec, prd));
ann_value_generation_by_tec(z,tec,prd) = sum(t, SD_balance_el.M(z,t) * q_gen.L(z, t, tec, prd));
ann_value_pumping(z) = sum((t,tec_strg), SD_balance_el.M(z,t) * q_store_in.L(z,t,tec_strg));
ann_value_turbining(z) = sum((t,tec_strg), SD_balance_el.M(z,t) * q_store_out.L(z,t,tec_strg));
ann_value_flows(z,zz) = sum(t, SD_balance_el.M(z,t) * flow.L(z,zz,t));
ann_value_curtail(z) = sum(t, SD_balance_el.M(z,t) * q_curtail.L(z,t));

display ann_value_generation, ann_value_generation_by_tec, ann_value_pumping, ann_value_turbining, ann_value_flows, ann_value_curtail;

******* prices, cost, producer surplus
parameter
annual_price_el(z),
annual_price_ht(z),
annual_cost(z,tec),
annual_revenue(z,tec),
annual_surplus_therm(z,tec),
annual_surplus_stor(z,tec_strg),
producer_surplus(z);

annual_price_el(z) = sum(t, SD_balance_el.M(z,t))/card(t);
annual_price_ht(z) = sum(t, SD_balance_ht.M(z,t))/card(t);
annual_cost(z,tec) = sum(t, cost_fuel.L(z,t,tec)
                         + cost_emission.L(z,t,tec)
                         + sum(prd, OM_VARIABLE_COST(tec) * q_gen.L(z,t,tec,prd)));
annual_revenue(z,tec) = sum(t,
                         SD_balance_el.M(z,t) * q_gen.L(z,t,tec,'el')
                         + SD_balance_ht.M(z,t) * q_gen.L(z,t,tec,'ht'));
annual_surplus_therm(z,tec) =   sum(t,
                         SD_balance_el.M(z,t) * q_gen.L(z,t,tec,'el')
                         + SD_balance_ht.M(z,t) * q_gen.L(z,t,tec,'ht')
                         - cost_fuel.L(z,t,tec)
                         - cost_emission.L(z,t,tec)
                         - sum(prd, OM_VARIABLE_COST(tec) * q_gen.L(z,t,tec,prd))
                         );
annual_surplus_stor(z,tec_strg) = sum(t,
                         SD_balance_el.M(z,t) * q_store_out.L(z,t,tec_strg)
                         - SD_balance_el.M(z,t) * q_store_in.L(z,t,tec_strg)
                         );
producer_surplus(z) =    sum(tec, annual_surplus_therm(z,tec))
                         + sum(tec_strg, annual_surplus_stor(z,tec_strg))
                         ;

display
annual_price_el, annual_price_ht, annual_cost, annual_surplus_therm, annual_surplus_stor, producer_surplus;

******* marginals of equations
parameter
hourly_price_el(z,t),
hourly_price_ht(z,t),
hourly_price_ancillary(z,t),
hourly_price_exports(z,zz,t)
;

hourly_price_el(z,t) = SD_balance_el.M(z,t);
hourly_price_ht(z,t) = SD_balance_ht.M(z,t);
hourly_price_ancillary(z,t) = ancillary_service.M(z,t);
hourly_price_exports(z,zz,t) = flow_balance.M(z,zz,t);
